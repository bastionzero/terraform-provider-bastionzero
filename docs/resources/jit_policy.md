---
page_title: "bastionzero_jit_policy Resource - terraform-provider-bastionzero"
subcategory: "Policy"
description: |-
  Provides a BastionZero just-in-time (JIT) policy. JIT policies provide temporary access to any BastionZero target.
---

# bastionzero_jit_policy (Resource)

Provides a BastionZero just-in-time (JIT) policy. JIT policies provide temporary access to any BastionZero target.

Learn more about JIT policies [here](https://docs.bastionzero.com/docs/admin-guide/authorization#just-in-time).

~> **Note on policy name** All policies (of any type) must have a unique name.
If the configured [`name`](#required) is not unique, an error is thrown.

~> **Note on child policies** A JIT policy's [`child_policies`](#child_policies)
can only refer to policies of the following types:
[`TargetConnect`](targetconnect_policy), [`Kubernetes`](kubernetes_policy), or
[`Proxy`](proxy_policy). If any of the referenced policies, are not of the valid
type, then an error is returned when creating/updating the JIT policy.

## Example Usage

Create a JIT policy that allows root/privileged access to select targets for the
"Engineering" group with approval by an administrator in one's BastionZero
organization:

```terraform
# Get all Bzero targets 
data "bastionzero_bzero_targets" "t" {}

locals {
  # Define, by name, the targets to add to the policy
  bzero_targets = ["prod-target"]
}

resource "bastionzero_targetconnect_policy" "child1" {
  name        = "child-tc-policy"
  description = "Child policy managed by Terraform."
  targets = [
    for each in data.bastionzero_bzero_targets.t.targets
    : { id = each.id, type = each.type } if contains(local.bzero_targets, each.name)
  ]

  # Notice, no subjects or groups are specified. We want to limit who has root
  # access via a just-in-time policy.

  # Permit access as the "root" user
  target_users = ["root"]
  verbs        = ["Shell", "FileTransfer", "Tunnel"]
}

# Get all Cluster targets
data "bastionzero_cluster_targets" "t" {}

locals {
  # Define, by name, the targets to add to the policy
  cluster_targets = ["prod-cluster"]
}

resource "bastionzero_kubernetes_policy" "child2" {
  name        = "child-kube-policy"
  description = "Child policy managed by Terraform."
  clusters = [
    for each in data.bastionzero_cluster_targets.t.targets
    : each.id if contains(local.cluster_targets, each.name)
  ]

  # Notice, no subjects or groups are specified. We want to limit who has
  # privileged access via a just-in-time policy.

  # Allow unrestricted rights to all Kubernetes APIs by permitting access to the
  # "system:masters" group which is a privileged group
  cluster_users  = ["cluster-admin"]
  cluster_groups = ["system:masters"]
}

# Get all users and groups
data "bastionzero_users" "u" {}
data "bastionzero_groups" "g" {}

locals {
  # Define, by email address, users to add to the policy
  users = ["alice@example.com", "bob@example.com", "charlie@example.com"]
  # Define, by name, the groups to add to the policy
  groups = ["Engineering"]
}

resource "bastionzero_jit_policy" "example" {
  name        = "example-policy"
  description = "Policy managed by Terraform."
  subjects = [
    for each in data.bastionzero_users.u.users
    : { id = each.id, type = each.type } if contains(local.users, each.email)
  ]
  groups = [
    for each in data.bastionzero_groups.g.groups
    : { id = each.id, name = each.name } if contains(local.groups, each.name)
  ]

  # Reference the policies created above
  child_policies = [
    for each in [
      bastionzero_targetconnect_policy.child1,
      bastionzero_kubernetes_policy.child2
    ]
    : { id = each.id }
  ]

  # Require approval by an admin
  auto_approved = false
  # Allow access only for 30 minutes
  duration = 30
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `child_policies` (Attributes Set) Set of policies that a JIT policy applies to. (see [below for nested schema](#nestedatt--child_policies))
- `name` (String) The policy's name.

### Optional

- `auto_approved` (Boolean) If true, then the policies created by this JIT policy will be automatically approved. If false, then policies will only be created based on request and approval from reviewers (Defaults to false).
- `description` (String) The policy's description.
- `duration` (Number) The amount of time (in minutes) after which the access granted by this JIT policy will expire (Defaults to 1 hour).
- `groups` (Attributes Set) Set of Identity Provider (IdP) groups that this policy applies to. (see [below for nested schema](#nestedatt--groups))
- `subjects` (Attributes Set) Set of subjects that this policy applies to. (see [below for nested schema](#nestedatt--subjects))

### Read-Only

- `id` (String) The policy's unique ID.
- `type` (String) The policy's type (constant value `JustInTime`).

<a id="nestedatt--child_policies"></a>
### Nested Schema for `child_policies`

Required:

- `id` (String) The policy's unique ID.

Read-Only:

- `name` (String) The policy's name.
- `type` (String) The policy's type (one of `TargetConnect`, `Kubernetes`, or `Proxy`).


<a id="nestedatt--groups"></a>
### Nested Schema for `groups`

Required:

- `id` (String) The group's unique ID.
- `name` (String) The group's name.


<a id="nestedatt--subjects"></a>
### Nested Schema for `subjects`

Required:

- `id` (String) The subject's unique ID.
- `type` (String) The subject's type (one of `User`, `ApiKey`, or `ServiceAccount`).

## Import

Import is supported using the following syntax:

```shell
# Policy can be imported by specifying the unique identifier.
terraform import bastionzero_jit_policy.example "d59c2333-b902-43b4-a009-7c34e714736c"
```