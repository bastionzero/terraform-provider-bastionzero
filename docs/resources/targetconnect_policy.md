---
page_title: "bastionzero_targetconnect_policy Resource - terraform-provider-bastionzero"
subcategory: "policy"
description: |-
  Provides a BastionZero target connect policy. Target connect policies provide access to Bzero and DynamicAccessConfig targets.
---

# bastionzero_targetconnect_policy (Resource)

Provides a BastionZero target connect policy. Target connect policies provide access to Bzero and DynamicAccessConfig targets.

-> **Note** All policies (of any type) must have a unique name. If the
configured name is not unique, an error is thrown.

## Example Usage

### Environment based policy

Create a target connect policy that allows access to targets in a set of
environments.

```terraform
# Get all users, groups, and environments 
data "bastionzero_users" "u" {}
data "bastionzero_groups" "g" {}
data "bastionzero_environments" "e" {}

locals {
  # Define, by email address, users to add to the policy
  users = ["alice@example.com", "bob@example.com", "charlie@example.com"]
  # Define, by name, the groups and environments to add to the policy
  groups = ["Engineering", "Product"]
  envs   = ["Default", "Demo-Env"]
}

resource "bastionzero_targetconnect_policy" "example" {
  name        = "example-policy"
  description = "Policy managed by Terraform."
  subjects = [
    for each in data.bastionzero_users.u.users
    : { id = each.id, type = each.type } if contains(local.users, each.email)
  ]
  groups = [
    for each in data.bastionzero_groups.g.groups
    : { id = each.id, name = each.name } if contains(local.groups, each.name)
  ]
  environments = [
    for each in data.bastionzero_environments.e.environments
    : each.id if contains(local.envs, each.name)
  ]

  # Permit access as "ec2-user"
  target_users = ["ec2-user"]
  # Allow shell access, file upload/download, and SSH
  verbs = ["Shell", "FileTransfer", "Tunnel"]
}
```

### Target based policy 

Create a target connect policy that allows access to specific targets.

```terraform
# Get all groups and Bzero targets 
data "bastionzero_groups" "g" {}
data "bastionzero_bzero_targets" "t" {}

locals {
  # Define, by name, the groups and targets to add to the policy
  groups  = ["Product", "Marketing"]
  targets = ["demo-1", "customer-insights"]
}

resource "bastionzero_targetconnect_policy" "example" {
  name        = "example-policy"
  description = "Policy managed by Terraform."
  groups = [
    for each in data.bastionzero_groups.g.groups
    : { id = each.id, name = each.name } if contains(local.groups, each.name)
  ]
  targets = [
    for each in data.bastionzero_bzero_targets.t.targets
    : { id = each.id, type = each.type } if contains(local.targets, each.name)
  ]

  # Permit access as "ec2-user" and "demo-user"
  target_users = ["ec2-user", "demo-user"]
  # Only allow shell access
  verbs = ["Shell"]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `name` (String) The policy's name.
- `target_users` (Set of String) Set of Unix usernames that this policy applies to.
- `verbs` (Set of String) Set of actions allowed by this policy (one of `Shell`, `FileTransfer`, or `Tunnel`).

### Optional

- `description` (String) The policy's description.
- `environments` (Set of String) Set of environments that this policy applies to.
- `groups` (Attributes Set) Set of Identity Provider (IdP) groups that this policy applies to. (see [below for nested schema](#nestedatt--groups))
- `subjects` (Attributes Set) Set of subjects that this policy applies to. (see [below for nested schema](#nestedatt--subjects))
- `targets` (Attributes Set) Set of targets that this policy applies to. (see [below for nested schema](#nestedatt--targets))

### Read-Only

- `id` (String) The policy's unique ID.
- `type` (String) The policy's type (constant value `TargetConnect`).

<a id="nestedatt--groups"></a>
### Nested Schema for `groups`

Required:

- `id` (String) The group's unique ID.
- `name` (String) The group's name.


<a id="nestedatt--subjects"></a>
### Nested Schema for `subjects`

Required:

- `id` (String) The subject's unique ID.
- `type` (String) The subject's type (one of `User`, `ApiKey`, or `ServiceAccount`).


<a id="nestedatt--targets"></a>
### Nested Schema for `targets`

Required:

- `id` (String) The target's unique ID.
- `type` (String) The target's type (one of `Bzero`, or `DynamicAccessConfig`).

## Import

Import is supported using the following syntax:

```shell
# Policy can be imported by specifying the unique identifier.
terraform import bastionzero_targetconnect_policy.example "ddda973a-6574-4ad5-bd9f-5250a1c2b5d7"
```