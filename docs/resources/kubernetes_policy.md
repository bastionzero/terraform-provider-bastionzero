---
page_title: "bastionzero_kubernetes_policy Resource - terraform-provider-bastionzero"
subcategory: "policy"
description: |-
  Provides a BastionZero Kubernetes policy. Kubernetes policies provide access to Cluster targets.
---

# bastionzero_kubernetes_policy (Resource)

Provides a BastionZero Kubernetes policy. Kubernetes policies provide access to Cluster targets.

Learn more about Kubernetes policies [here](https://docs.bastionzero.com/docs/admin-guide/authorization#kubernetes).

~> **Note on policy name** All policies (of any type) must have a unique name. If the
configured [`name`](#name) is not unique, an error is thrown.

## Example Usage

### Environment based policy

Create a Kubernetes policy that allows access to targets in a set of
environments.

```terraform
# Get all users, groups, and environments 
data "bastionzero_users" "u" {}
data "bastionzero_groups" "g" {}
data "bastionzero_environments" "e" {}

locals {
  # Define, by email address, users to add to the policy
  users = ["alice@example.com", "bob@example.com", "charlie@example.com"]
  # Define, by name, the groups and environments to add to the policy
  groups = ["Engineering", "Product"]
  envs   = ["Default", "Demo-Env"]
}

resource "bastionzero_kubernetes_policy" "example" {
  name        = "example-policy"
  description = "Policy managed by Terraform."
  subjects = [
    for each in data.bastionzero_users.u.users
    : { id = each.id, type = each.type } if contains(local.users, each.email)
  ]
  groups = [
    for each in data.bastionzero_groups.g.groups
    : { id = each.id, name = each.name } if contains(local.groups, each.name)
  ]
  environments = [
    for each in data.bastionzero_environments.e.environments
    : each.id if contains(local.envs, each.name)
  ]

  # Permit access as the "viewer" Kubernetes RBAC user, and therefore assume all
  # the permissions granted to the "viewer" user as defined by in-cluster
  # RoleBindings and ClusterRoleBindings
  cluster_users = ["viewer"]
}
```

### Target based policy 

Create a Kubernetes policy that allows access to specific targets.

```terraform
# Get all groups and Cluster targets 
data "bastionzero_groups" "g" {}
data "bastionzero_cluster_targets" "t" {}

locals {
  # Define, by name, the groups and targets to add to the policy
  groups  = ["Administrators"]
  targets = ["prod-cluster", "stage-cluster"]
}

resource "bastionzero_kubernetes_policy" "example" {
  name        = "example-policy"
  description = "Policy managed by Terraform."
  groups = [
    for each in data.bastionzero_groups.g.groups
    : { id = each.id, name = each.name } if contains(local.groups, each.name)
  ]
  clusters = [
    for each in data.bastionzero_cluster_targets.t.targets
    : each.id if contains(local.targets, each.name)
  ]

  # Allow unrestricted rights to all Kubernetes APIs by permitting access to the
  # "system:masters" group which is a privileged group
  cluster_users  = ["cluster-admin"]
  cluster_groups = ["system:masters"]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `name` (String) The policy's name.

### Optional

- `cluster_groups` (Set of String) Set of Kubernetes RBAC subject group names that this policy applies to. See https://kubernetes.io/docs/reference/access-authn-authz/rbac/#referring-to-subjects for more details.
- `cluster_users` (Set of String) Set of Kubernetes RBAC subject usernames that this policy applies to. See https://kubernetes.io/docs/reference/access-authn-authz/rbac/#referring-to-subjects for more details.
- `clusters` (Set of String) Set of Cluster target ID(s) that this policy applies to.
- `description` (String) The policy's description.
- `environments` (Set of String) Set of environments that this policy applies to.
- `groups` (Attributes Set) Set of Identity Provider (IdP) groups that this policy applies to. (see [below for nested schema](#nestedatt--groups))
- `subjects` (Attributes Set) Set of subjects that this policy applies to. (see [below for nested schema](#nestedatt--subjects))

### Read-Only

- `id` (String) The policy's unique ID.
- `type` (String) The policy's type (constant value `Kubernetes`).

<a id="nestedatt--groups"></a>
### Nested Schema for `groups`

Required:

- `id` (String) The group's unique ID.
- `name` (String) The group's name.


<a id="nestedatt--subjects"></a>
### Nested Schema for `subjects`

Required:

- `id` (String) The subject's unique ID.
- `type` (String) The subject's type (one of `User`, `ApiKey`, or `ServiceAccount`).

## Import

Import is supported using the following syntax:

```shell
# Policy can be imported by specifying the unique identifier.
terraform import bastionzero_kubernetes_policy.example "a09aba46-db60-4b95-9b42-c536701d4f7a"
```